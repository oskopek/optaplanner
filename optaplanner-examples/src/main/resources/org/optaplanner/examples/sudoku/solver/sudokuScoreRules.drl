/*
 * Copyright 2010 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaplanner.examples.sudoku.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.simple.SimpleScoreHolder;
import org.optaplanner.examples.sudoku.domain.SudokuNumber;

global SimpleScoreHolder scoreHolder;

// ############################################################################
// Constraints
// ############################################################################

rule "equalSudokuNumbersHorizontal"
    when
        SudokuNumber($id : id, row != null, $i : rowIndex, $num : number)
        SudokuNumber(id > $id, rowIndex == $i, number == $num)
    then
        scoreHolder.addConstraintMatch(kcontext, -1);
end

rule "equalSudokuNumbersVertical"
    when
        SudokuNumber($id : id, column != null, $i : columnIndex, $num : number)
        SudokuNumber(id > $id, columnIndex == $i, number == $num)
    then
        scoreHolder.addConstraintMatch(kcontext, -1);
end

rule "equalSudokuNumbersSquare"
    when
        SudokuNumber($id : id, square != null, $i : squareIndex, $num : number)
        SudokuNumber(id > $id, squareIndex == $i, number == $num)
    then
        scoreHolder.addConstraintMatch(kcontext, -1);
end
